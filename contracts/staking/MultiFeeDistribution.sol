pragma solidity 0.7.6;
pragma abicoder v2;

import '../interfaces/IChefIncentivesController.sol';
import '../interfaces/IMultiFeeDistribution.sol';
import '../dependencies/openzeppelin/contracts/IERC20.sol';
import '../dependencies/openzeppelin/contracts/SafeERC20.sol';
import '../dependencies/openzeppelin/contracts/SafeMath.sol';
import '../misc/OwnableUpgradable.sol';
import {Errors} from '../protocol/libraries/helpers/Errors.sol';

interface IMintableToken is IERC20 {
  function mint(address _receiver, uint256 _amount) external returns (bool);

  function setMinter(address _minter) external returns (bool);
}

// Based on Ellipsis EPS Staker
// https://github.com/ellipsis-finance/ellipsis/blob/master/contracts/EpsStaker.sol
contract MultiFeeDistribution is IMultiFeeDistribution, OwnableUpgradable {
  using SafeMath for uint256;
  using SafeERC20 for IERC20;
  using SafeERC20 for IMintableToken;

  /* ========== STATE VARIABLES ========== */

  struct Reward {
    uint256 periodFinish;
    uint256 rewardRate;
    uint256 lastUpdateTime;
    uint256 rewardPerTokenStored;
    // tracks already-added balances to handle accrued interest in aToken rewards
    // for the stakingToken this value is unused and will always be 0
    uint256 balance;
  }
  struct Balances {
    uint256 total;
    uint256 unlocked;
    uint256 locked;
    uint256 earned;
  }
  struct LockedBalance {
    uint256 amount;
    uint256 unlockTime;
  }
  struct RewardData {
    address token;
    uint256 amount;
  }

  uint256 public constant VERSION = 0x1;

  IChefIncentivesController public incentivesController;
  IMintableToken public stakingToken;
  address[] public rewardTokens;
  mapping(address => Reward) public rewardData;

  // Duration that rewards are streamed over
  uint256 public constant rewardsDuration = 86400 * 7;

  // Duration of lock/earned penalty period
  uint256 public constant lockDuration = rewardsDuration * 13;

  // Addresses approved to call mint
  mapping(address => bool) public minters;
  bool public mintersAreSet;

  // user -> reward token -> amount
  mapping(address => mapping(address => uint256)) public userRewardPerTokenPaid;
  mapping(address => mapping(address => uint256)) public rewards;

  uint256 public totalSupply;
  uint256 public lockedSupply;

  // Private mappings for balance data
  mapping(address => Balances) private balances;
  mapping(address => LockedBalance[]) private userLocks;
  mapping(address => LockedBalance[]) private userEarnings;

  bool private _paused;

  modifier whenNotPaused() {
    require(!_paused, "CLAIMING_PAUSED");
    _;
  }

  /* ========== INITIALIZER ========== */

  function initialize(address owner, address _stakingToken) external initializer {
    _transferOwnership(owner);

    stakingToken = IMintableToken(_stakingToken);
    IMintableToken(_stakingToken).setMinter(address(this));
    // First reward MUST be the staking token or things will break
    // related to the 50% penalty and distribution to locked balances
    rewardTokens.push(_stakingToken);
    rewardData[_stakingToken].lastUpdateTime = block.timestamp;
  
    _paused = true;
  }

  /* ========== ADMIN CONFIGURATION ========== */

  function setMinters(address[] memory _minters) external onlyOwner {
    require(!mintersAreSet);
    for (uint256 i; i < _minters.length; i++) {
      minters[_minters[i]] = true;
    }
    mintersAreSet = true;
  }

  function setIncentivesController(IChefIncentivesController _controller) external onlyOwner {
    incentivesController = _controller;
  }

  // Add a new reward token to be distributed to stakers
  function addReward(address _rewardsToken) external override onlyOwner {
    require(rewardData[_rewardsToken].lastUpdateTime == 0);
    rewardTokens.push(_rewardsToken);
    rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
    rewardData[_rewardsToken].periodFinish = block.timestamp;
  }

  function setPause(bool val) external onlyOwner {
    _paused = val;
    if (_paused) {
      emit Paused();
    } else {
      emit Unpaused();
    }
  }

  /* ========== VIEWS ========== */

  function _rewardPerToken(address _rewardsToken, uint256 _supply) internal view returns (uint256) {
    if (_supply == 0) {
      return rewardData[_rewardsToken].rewardPerTokenStored;
    }
    return
      rewardData[_rewardsToken].rewardPerTokenStored.add(
        lastTimeRewardApplicable(_rewardsToken)
          .sub(rewardData[_rewardsToken].lastUpdateTime)
          .mul(rewardData[_rewardsToken].rewardRate)
          .mul(1e18)
          .div(_supply)
      );
  }

  function _earned(
    address _user,
    address _rewardsToken,
    uint256 _balance,
    uint256 _currentRewardPerToken
  ) internal view returns (uint256) {
    return
      _balance
        .mul(_currentRewardPerToken.sub(userRewardPerTokenPaid[_user][_rewardsToken]))
        .div(1e18)
        .add(rewards[_user][_rewardsToken]);
  }

  function lastTimeRewardApplicable(address _rewardsToken) public view returns (uint256) {
    uint256 periodFinish = rewardData[_rewardsToken].periodFinish;
    return block.timestamp < periodFinish ? block.timestamp : periodFinish;
  }

  function rewardPerToken(address _rewardsToken) external view returns (uint256) {
    uint256 supply = _rewardsToken == address(stakingToken) ? lockedSupply : totalSupply;
    return _rewardPerToken(_rewardsToken, supply);
  }

  function getRewardForDuration(address _rewardsToken) external view returns (uint256) {
    return rewardData[_rewardsToken].rewardRate.mul(rewardsDuration).div(1e12);
  }

  // Address and claimable amount of all reward tokens for the given account
  function claimableRewards(address account) external view returns (RewardData[] memory rewards) {
    rewards = new RewardData[](rewardTokens.length);
    for (uint256 i = 0; i < rewards.length; i++) {
      // If i == 0 this is the stakingReward, distribution is based on locked balances
      uint256 balance = i == 0 ? balances[account].locked : balances[account].total;
      uint256 supply = i == 0 ? lockedSupply : totalSupply;
      rewards[i].token = rewardTokens[i];
      rewards[i].amount = _earned(
        account,
        rewards[i].token,
        balance,
        _rewardPerToken(rewardTokens[i], supply)
      ).div(1e12);
    }
    return rewards;
  }

  // Total balance of an account, including unlocked, locked and earned tokens
  function totalBalance(address user) external view returns (uint256 amount) {
    return balances[user].total;
  }

  // Total withdrawable balance for an account to which no penalty is applied
  function unlockedBalance(address user) external view returns (uint256 amount) {
    amount = balances[user].unlocked;
    LockedBalance[] storage earnings = userEarnings[msg.sender];
    for (uint256 i = 0; i < earnings.length; i++) {
      if (earnings[i].unlockTime > block.timestamp) {
        break;
      }
      amount = amount.add(earnings[i].amount);
    }
    return amount;
  }

  // Information on the "earned" balances of a user
  // Earned balances may be withdrawn immediately for a 50% penalty
  function earnedBalances(
    address user
  ) external view returns (uint256 total, LockedBalance[] memory earningsData) {
    LockedBalance[] storage earnings = userEarnings[user];
    uint256 idx;
    for (uint256 i = 0; i < earnings.length; i++) {
      if (earnings[i].unlockTime > block.timestamp) {
        if (idx == 0) {
          earningsData = new LockedBalance[](earnings.length - i);
        }
        earningsData[idx] = earnings[i];
        idx++;
        total = total.add(earnings[i].amount);
      }
    }
    return (total, earningsData);
  }

  // Information on a user's locked balances
  function lockedBalances(
    address user
  )
    external
    view
    returns (uint256 total, uint256 unlockable, uint256 locked, LockedBalance[] memory lockData)
  {
    LockedBalance[] storage locks = userLocks[user];
    uint256 idx;
    for (uint256 i = 0; i < locks.length; i++) {
      if (locks[i].unlockTime > block.timestamp) {
        if (idx == 0) {
          lockData = new LockedBalance[](locks.length - i);
        }
        lockData[idx] = locks[i];
        idx++;
        locked = locked.add(locks[i].amount);
      } else {
        unlockable = unlockable.add(locks[i].amount);
      }
    }
    return (balances[user].locked, unlockable, locked, lockData);
  }

  // Final balance received and penalty balance paid by user upon calling exit
  function withdrawableBalance(
    address user
  ) public view returns (uint256 amount, uint256 penaltyAmount) {
    Balances storage bal = balances[user];
    uint256 earned = bal.earned;
    if (earned > 0) {
      uint256 amountWithoutPenalty;
      uint256 length = userEarnings[user].length;
      for (uint256 i = 0; i < length; i++) {
        uint256 earnedAmount = userEarnings[user][i].amount;
        if (earnedAmount == 0) continue;
        if (userEarnings[user][i].unlockTime > block.timestamp) {
          break;
        }
        amountWithoutPenalty = amountWithoutPenalty.add(earnedAmount);
      }

      penaltyAmount = earned.sub(amountWithoutPenalty).div(2);
    }
    amount = bal.unlocked.add(earned).sub(penaltyAmount);
    return (amount, penaltyAmount);
  }

  function paused() external view returns (bool) {
    return _paused;
  }

  /* ========== MUTATIVE FUNCTIONS ========== */

  // Stake tokens to receive rewards
  // Locked tokens cannot be withdrawn for lockDuration and are eligible to receive stakingReward rewards
  function stake(uint256 amount, bool lock) external {
    require(amount > 0, 'Cannot stake 0');
    _updateReward(msg.sender);
    totalSupply = totalSupply.add(amount);
    Balances storage bal = balances[msg.sender];
    bal.total = bal.total.add(amount);
    if (lock) {
      lockedSupply = lockedSupply.add(amount);
      bal.locked = bal.locked.add(amount);
      uint256 unlockTime = block.timestamp.div(rewardsDuration).mul(rewardsDuration).add(
        lockDuration
      );
      uint256 idx = userLocks[msg.sender].length;
      if (idx == 0 || userLocks[msg.sender][idx - 1].unlockTime < unlockTime) {
        userLocks[msg.sender].push(LockedBalance({amount: amount, unlockTime: unlockTime}));
      } else {
        userLocks[msg.sender][idx - 1].amount = userLocks[msg.sender][idx - 1].amount.add(amount);
      }
    } else {
      bal.unlocked = bal.unlocked.add(amount);
    }
    stakingToken.safeTransferFrom(msg.sender, address(this), amount);
    emit Staked(msg.sender, amount, lock);
  }

  // Mint new tokens
  // Minted tokens receive rewards normally but incur a 50% penalty when
  // withdrawn before lockDuration has passed.
  function mint(address user, uint256 amount, bool withPenalty) external override {
    require(minters[msg.sender]);
    if (amount == 0) return;
    _updateReward(user);
    stakingToken.mint(address(this), amount);
    if (user == address(this)) {
      // minting to this contract adds the new tokens as incentives for lockers
      _notifyReward(address(stakingToken), amount);
      return;
    }
    totalSupply = totalSupply.add(amount);
    Balances storage bal = balances[user];
    bal.total = bal.total.add(amount);
    if (withPenalty) {
      bal.earned = bal.earned.add(amount);
      uint256 unlockTime = block.timestamp.div(rewardsDuration).mul(rewardsDuration).add(
        lockDuration
      );
      LockedBalance[] storage earnings = userEarnings[user];
      uint256 idx = earnings.length;
      if (idx == 0 || earnings[idx - 1].unlockTime < unlockTime) {
        earnings.push(LockedBalance({amount: amount, unlockTime: unlockTime}));
      } else {
        earnings[idx - 1].amount = earnings[idx - 1].amount.add(amount);
      }
    } else {
      bal.unlocked = bal.unlocked.add(amount);
    }
    emit Staked(user, amount, false);
  }

  // Withdraw staked tokens
  // First withdraws unlocked tokens, then earned tokens. Withdrawing earned tokens
  // incurs a 50% penalty which is distributed based on locked balances.
  function withdraw(uint256 amount) public {
    require(amount > 0, 'Cannot withdraw 0');
    _updateReward(msg.sender);
    Balances storage bal = balances[msg.sender];
    uint256 penaltyAmount;

    if (amount <= bal.unlocked) {
      bal.unlocked = bal.unlocked.sub(amount);
    } else {
      uint256 remaining = amount.sub(bal.unlocked);
      require(bal.earned >= remaining, 'Insufficient unlocked balance');
      bal.unlocked = 0;
      bal.earned = bal.earned.sub(remaining);
      for (uint256 i = 0; ; i++) {
        uint256 earnedAmount = userEarnings[msg.sender][i].amount;
        if (earnedAmount == 0) continue;
        if (penaltyAmount == 0 && userEarnings[msg.sender][i].unlockTime > block.timestamp) {
          penaltyAmount = remaining;
          require(bal.earned >= remaining, 'Insufficient balance after penalty');
          bal.earned = bal.earned.sub(remaining);
          if (bal.earned == 0) {
            delete userEarnings[msg.sender];
            break;
          }
          remaining = remaining.mul(2);
        }
        if (remaining <= earnedAmount) {
          userEarnings[msg.sender][i].amount = earnedAmount.sub(remaining);
          break;
        } else {
          delete userEarnings[msg.sender][i];
          remaining = remaining.sub(earnedAmount);
        }
      }
    }

    uint256 adjustedAmount = amount.add(penaltyAmount);
    bal.total = bal.total.sub(adjustedAmount);
    totalSupply = totalSupply.sub(adjustedAmount);
    stakingToken.safeTransfer(msg.sender, amount);
    if (penaltyAmount > 0) {
      incentivesController.claim(address(this), new address[](0));
      _notifyReward(address(stakingToken), penaltyAmount);
    }
    emit Withdrawn(msg.sender, amount, penaltyAmount);
  }

  function _getReward(address[] memory _rewardTokens) internal whenNotPaused {
    uint256 length = _rewardTokens.length;
    for (uint256 i; i < length; i++) {
      address token = _rewardTokens[i];
      uint256 reward = rewards[msg.sender][token].div(1e12);
      if (token != address(stakingToken)) {
        // for rewards other than stakingToken, every 24 hours we check if new
        // rewards were sent to the contract or accrued via aToken interest
        Reward storage r = rewardData[token];
        uint256 periodFinish = r.periodFinish;
        require(periodFinish > 0, 'Unknown reward token');
        uint256 balance = r.balance;
        if (periodFinish < block.timestamp.add(rewardsDuration - 86400)) {
          uint256 unseen = IERC20(token).balanceOf(address(this)).sub(balance);
          if (unseen > 0) {
            _notifyReward(token, unseen);
            balance = balance.add(unseen);
          }
        }
        r.balance = balance.sub(reward);
      }
      if (reward == 0) continue;
      rewards[msg.sender][token] = 0;
      IERC20(token).safeTransfer(msg.sender, reward);
      emit RewardPaid(msg.sender, token, reward);
    }
  }

  // Claim all pending staking rewards
  function getReward() public {
    _updateReward(msg.sender);
    _getReward(rewardTokens);
  }

  function getReward(address[] memory _rewardTokens) public {
    _updateReward(msg.sender);
    _getReward(_rewardTokens);
  }

  // Withdraw full unlocked balance and optionally claim pending rewards
  function exit(bool claimRewards) external {
    _updateReward(msg.sender);
    (uint256 amount, uint256 penaltyAmount) = withdrawableBalance(msg.sender);
    delete userEarnings[msg.sender];
    Balances storage bal = balances[msg.sender];
    bal.total = bal.total.sub(bal.unlocked).sub(bal.earned);
    bal.unlocked = 0;
    bal.earned = 0;

    totalSupply = totalSupply.sub(amount.add(penaltyAmount));
    stakingToken.safeTransfer(msg.sender, amount);
    if (penaltyAmount > 0) {
      incentivesController.claim(address(this), new address[](0));
      _notifyReward(address(stakingToken), penaltyAmount);
    }
    if (claimRewards) {
      _getReward(rewardTokens);
    }
    emit Withdrawn(msg.sender, amount, penaltyAmount);
  }

  // Withdraw all currently locked tokens where the unlock time has passed
  function withdrawExpiredLocks() external {
    _updateReward(msg.sender);
    LockedBalance[] storage locks = userLocks[msg.sender];
    Balances storage bal = balances[msg.sender];
    uint256 amount;
    uint256 length = locks.length;
    if (locks[length - 1].unlockTime <= block.timestamp) {
      amount = bal.locked;
      delete userLocks[msg.sender];
    } else {
      for (uint256 i = 0; i < length; i++) {
        if (locks[i].unlockTime > block.timestamp) break;
        amount = amount.add(locks[i].amount);
        delete locks[i];
      }
    }
    bal.locked = bal.locked.sub(amount);
    bal.total = bal.total.sub(amount);
    totalSupply = totalSupply.sub(amount);
    lockedSupply = lockedSupply.sub(amount);
    stakingToken.safeTransfer(msg.sender, amount);
    emit Withdrawn(msg.sender, amount, 0);
  }

  /* ========== RESTRICTED FUNCTIONS ========== */

  function _notifyReward(address _rewardsToken, uint256 reward) internal {
    Reward storage r = rewardData[_rewardsToken];
    if (block.timestamp >= r.periodFinish) {
      r.rewardRate = reward.mul(1e12).div(rewardsDuration);
    } else {
      uint256 remaining = r.periodFinish.sub(block.timestamp);
      uint256 leftover = remaining.mul(r.rewardRate).div(1e12);
      r.rewardRate = reward.add(leftover).mul(1e12).div(rewardsDuration);
    }

    r.lastUpdateTime = block.timestamp;
    r.periodFinish = block.timestamp.add(rewardsDuration);
  }

  // Added to support recovering LP Rewards from other systems such as BAL to be distributed to holders
  function recoverERC20(address tokenAddress, uint256 tokenAmount) external onlyOwner {
    require(tokenAddress != address(stakingToken), 'Cannot withdraw staking token');
    require(rewardData[tokenAddress].lastUpdateTime == 0, 'Cannot withdraw reward token');
    IERC20(tokenAddress).safeTransfer(owner(), tokenAmount);
    emit Recovered(tokenAddress, tokenAmount);
  }

  function _updateReward(address account) internal {
    address token = address(stakingToken);
    uint256 balance;
    Reward storage r = rewardData[token];
    uint256 rpt = _rewardPerToken(token, lockedSupply);
    r.rewardPerTokenStored = rpt;
    r.lastUpdateTime = lastTimeRewardApplicable(token);
    if (account != address(this)) {
      // Special case, use the locked balances and supply for stakingReward rewards
      rewards[account][token] = _earned(account, token, balances[account].locked, rpt);
      userRewardPerTokenPaid[account][token] = rpt;
      balance = balances[account].total;
    }

    uint256 supply = totalSupply;
    uint256 length = rewardTokens.length;
    for (uint256 i = 1; i < length; i++) {
      token = rewardTokens[i];
      r = rewardData[token];
      rpt = _rewardPerToken(token, supply);
      r.rewardPerTokenStored = rpt;
      r.lastUpdateTime = lastTimeRewardApplicable(token);
      if (account != address(this)) {
        rewards[account][token] = _earned(account, token, balance, rpt);
        userRewardPerTokenPaid[account][token] = rpt;
      }
    }
  }

  /* ========== EVENTS ========== */

  event RewardAdded(uint256 reward);
  event Staked(address indexed user, uint256 amount, bool locked);
  event Withdrawn(address indexed user, uint256 receivedAmount, uint256 penaltyPaid);
  event RewardPaid(address indexed user, address indexed rewardsToken, uint256 reward);
  event RewardsDurationUpdated(address token, uint256 newDuration);
  event Recovered(address token, uint256 amount);
}
